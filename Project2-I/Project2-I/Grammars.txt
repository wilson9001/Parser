datalogProgram	->	SCHEMES COLON scheme schemeList
		        FACTS COLON factList
		        RULES COLON ruleList
		        QUERIES COLON query queryList

scheme   	-> 	ID LEFT_PAREN ID idList RIGHT_PAREN

schemeList	->	scheme schemeList | lambda

idList  	->	COMMA ID idList | lambda

fact    	-> 	ID LEFT_PAREN STRING stringList RIGHT_PAREN PERIOD

factList	->	fact factList | lambda

rule    	->	headPredicate COLON_DASH predicate predicateList PERIOD

ruleList	->	rule ruleList | lambda

headPredicate	->	ID LEFT_PAREN ID idList RIGHT_PAREN

predicate	->	ID LEFT_PAREN parameter parameterList RIGHT_PAREN

predicateList	->	COMMA predicate predicateList | lambda

parameter	->	STRING | ID | expression

parameterList	-> 	COMMA parameter parameterList | lambda

expression	-> 	LEFT_PAREN parameter operator parameter RIGHT_PAREN

operator	->	ADD | MULTIPLY

query	        ->      predicate Q_MARK

queryList	->	query queryList | lambda

stringList	->	COMMA STRING stringList | lambda